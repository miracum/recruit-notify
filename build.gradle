plugins {
    id "org.springframework.boot" version "2.6.0"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.github.spotbugs" version "4.7.10"
    id "java"
    id "checkstyle"
    id "jacoco"
    id "com.diffplug.spotless" version "6.0.0"
    id "io.freefair.lombok" version "6.3.0"
}

group = "org.miracum.recruit"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "17"
targetCompatibility = "17"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set("springCloudVersion", "2020.0.4")
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-quartz"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "org.springframework.retry:spring-retry:1.3.1"
    implementation "io.micrometer:micrometer-registry-prometheus:1.8.0"
    implementation "io.micrometer:micrometer-core:1.8.0"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:5.6.0"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client:5.6.0"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:5.6.0"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client-okhttp:5.6.0"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client-okhttp:5.6.0"

    implementation "io.jaegertracing:jaeger-client:1.6.0"

    implementation "net.logstash.logback:logstash-logback-encoder:7.0.1"

    implementation "io.opentracing.contrib:opentracing-spring-web-starter:4.1.0"
    implementation "io.opentracing.contrib:opentracing-spring-jaeger-web-starter:3.3.1"
    implementation "io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:3.3.1"
    implementation "io.opentracing.contrib:opentracing-okhttp3:3.0.0"

    runtimeOnly "org.postgresql:postgresql:42.3.1"

    developmentOnly "org.springframework.boot:spring-boot-devtools"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "ognl:ognl:3.3.0"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

// Mitigation for CVE-2021-44228
// https://spring.io/blog/2021/12/10/log4j2-vulnerability-and-spring-boot
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == "org.apache.logging.log4j") {
            details.useVersion "2.15.0"
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled true
        csv.destination file("${buildDir}/jacoco/coverage.csv")
        html.destination file("${buildDir}/jacoco/html")
    }
}

spotbugs {
    excludeFilter = file(".spotbugs-exclude.xml")
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion "8.45"
    configFile = file(".checkstyle/checkstyle.xml")
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}
