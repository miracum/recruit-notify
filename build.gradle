plugins {
    id "org.springframework.boot" version "2.4.0"
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    id "com.github.spotbugs" version "4.6.0"
    id "java"
    id "checkstyle"
    id "jacoco"
    id "com.diffplug.spotless" version "5.8.2"
    id "io.freefair.lombok" version "5.3.0"
}

group = "org.miracum.recruit"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "11"

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.retry:spring-retry:1.3.0"
    implementation "io.micrometer:micrometer-registry-prometheus:1.6.1"
    implementation "io.micrometer:micrometer-core:1.6.1"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:5.2.0"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client:5.2.0"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:5.2.0"
    implementation "org.quartz-scheduler:quartz:2.3.2"
    implementation "org.springframework:spring-tx:5.2.9.RELEASE"
    developmentOnly "org.springframework.boot:spring-boot-devtools:2.4.0"

    testImplementation("org.springframework.boot:spring-boot-starter-test:2.4.0") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "ognl:ognl:3.2.16"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/jacoco-reports")
}

jacocoTestReport {
    reports {
        html.enabled false
        xml.enabled false
        csv.enabled true
        csv.destination file("${buildDir}/jacoco/coverage.csv")
    }
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
        }
    }
}

test {
    useJUnitPlatform()
}

bootJar {
    layered()
}

checkstyle {
    toolVersion "8.29"
    configFile = file(".checkstyle/checkstyle.xml")
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}
