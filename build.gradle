plugins {
    id "org.springframework.boot" version "2.4.5"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.github.spotbugs" version "4.2.3"
    id "java"
    id "checkstyle"
    id "jacoco"
    id "com.diffplug.spotless" version "5.12.4"
    id "io.freefair.lombok" version "5.3.3.3"
}

group = "org.miracum.recruit"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "11"

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

ext {
    set("springCloudVersion", "2020.0.1")
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-quartz"
    implementation "org.springframework.retry:spring-retry:1.3.1"
    implementation "io.micrometer:micrometer-registry-prometheus:1.6.6"
    implementation "io.micrometer:micrometer-core:1.6.6"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:5.3.2"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client:5.3.2"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:5.3.2"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client-okhttp:5.3.2"
    developmentOnly "org.springframework.boot:spring-boot-devtools:2.4.5"

    implementation "io.jaegertracing:jaeger-client:1.6.0"

    implementation "net.logstash.logback:logstash-logback-encoder:6.6"

    implementation "io.opentracing.contrib:opentracing-spring-web-starter:4.1.0"
    implementation "io.opentracing.contrib:opentracing-spring-jaeger-web-starter:3.3.1"
    implementation "io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:3.3.1"
    implementation "io.opentracing.contrib:opentracing-okhttp3:3.0.0"

    testImplementation("org.springframework.boot:spring-boot-starter-test:2.4.5") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "ognl:ognl:3.2.20"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled true
        csv.destination file("${buildDir}/jacoco/coverage.csv")
        html.destination file("${buildDir}/jacoco/html")
    }
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
        }
    }
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion "8.42"
    configFile = file(".checkstyle/checkstyle.xml")
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
    }
}
